buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.jruyi.gradle:thrift-gradle-plugin:0.2.0'
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.51'
        compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.0.2'
        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.0.2'
        compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.0.2'
        testCompile group: 'junit', name: 'junit', version: '4.+'
    }

    group = 'at.ac.tuwien.aic.ws14.group2.onion'
    version = '0.1.0'

    idea.module.jdkName = '1.8'
}

project(':node:common') {
    dependencies {
        compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.51'
		compile project(':directory-api')
	}
}

project(':node:chain') {
    apply plugin: 'application'

    mainClassName = group+'.node.chain.HelloChain'

    dependencies {
        compile project(':node:common')
    }
}

project(':node:local') {
    apply plugin: 'application'

    mainClassName = group+'.node.local.HelloLocal'

    dependencies {
        compile project(':node:common')
    }
}

project(':directory') {
    apply plugin: 'application'

    mainClassName = group+'.directory.HelloDirectory'

    dependencies {
        compile project(':directory-api')
    }
}

project(':directory-api') {
    apply plugin: 'org.jruyi.thrift'

    dependencies {
        compile group: 'org.apache.thrift', name: 'libthrift', version: '0.9.1'
    }

    idea.module {
        excludeDirs -= file(buildDir)
        buildDir.listFiles({d,f -> f != 'generated-sources'} as FilenameFilter).each {excludeDirs += it}
    }

    compileThrift {
        recurse true

        generator 'java', 'private-members'
    }
}

project(':target') {
    apply plugin: 'application'

    mainClassName = group+'.target.HelloTarget'
}
