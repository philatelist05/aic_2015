buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.jruyi.gradle:thrift-gradle-plugin:0.2.0'
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.51'
        compile group: 'org.apache.thrift', name: 'libthrift', version: '0.9.1'
        compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.0.2'
        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.0.2'
        compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.0.2'
        testCompile group: 'junit', name: 'junit', version: '4.+'
    }

    group = 'at.ac.tuwien.aic.ws14.group2.onion'
    version = '0.1.0'

    idea.module.jdkName = '1.8'
}

project(':onion-chainnode') {
    apply plugin: 'application'

    mainClassName = 'at.ac.tuwien.aic.ws14.group2.onion.chain.HelloChain'

    dependencies {
        compile project(':onion-common')
    }
}

project(':onion-directorynode') {
    apply plugin: 'application'

    mainClassName = 'at.ac.tuwien.aic.ws14.group2.onion.directory.HelloDirectory'

    dependencies {
        compile project(':onion-common')
    }
}

project(':onion-common') {
    apply plugin: 'org.jruyi.thrift'

    idea.module {
        excludeDirs -= file(buildDir)
        sourceDirs += file('build/generated-src/thrift/gen-java')
    }

    compileThrift {
        recurse true

        generator 'java', 'private-members'
    }
}
